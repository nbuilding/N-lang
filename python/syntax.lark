start: (instruction ((";" | "\n") instruction)*)

instruction: declare (";")?
           | print (";")?
           | function_callback (";")?
           | function_callback_quirky (";")?
           | function_callback_quirky_pipe (";")?
           | for (";")?
           | imp (";")?
           | return (";")?
           | imported_command (";")?
           | if (";")?
           | ifelse (";")?
           | vary (";")?

non_infix_boolval: value
                 | function_callback
                 | function_callback_quirky
                 | function_callback_quirky_pipe

// functions
vary: "var " NAME "=" (expression | function_def | anonymous_func)
declare: "let " name_type "=" (expression | function_def | anonymous_func)
print: "print" value
function_callback: "<" (function_call | anonymous_function_call) ">"
function_callback_quirky: value ".<" (function_call | anonymous_function_call) ">"
function_callback_quirky_pipe: value "\n" "|>" "<" (function_call | anonymous_function_call) ">"
for: "for" name_type NUMBER code_block
imp: "import" NAME
return: "return" expression
imported_command: "<" NAME "." NAME (" " [value (" " value)*])? ">"
if: "if" expression ("{"? value "}"? | "{"? instruction "}"? | code_block)
ifelse: "if" expression ("{"? value "}"? | "{"? instruction "}"? | code_block) "else" ("{"? instruction "}"? | code_block)
ifelse_expr: "if" expression "{"? expression "}"? "else" "{"? expression "}"?

//helpers
name_type: NAME [":" types]
function_dec_call: NAME (" " [name_type (" " name_type)*])?
function_call : value (" " value)*
anonymous_function_call : anonymous_func (" " value)*
code_block: "{" instruction* "}"
function_def: arguments ["->" NAME] code_block
arguments: "[" (name_type (" " name_type)*)? "]"
anonymous_func: "(" arguments "->" NAME ":" (instruction (";" instruction)*) ")"
char: "\\" ( escape_code | "{" /./ "}")
escape_code: /[tnr]/
tupledef: "(" NAME ("," NAME)* ")"
tupleval: "(" value ("," value)* ")"

// Boolean and number expressions, with order of operations.
// Question mark "inlines" the branch, so we don't get nested
// boolean_expression(or_expression(and_expression etc)).
// Square brackets mean that the stuff inside it can appear 0 or 1 time. Same as
// (whatever)?

?types: NAME
      | tupledef

?expression: ifelse_expr
           | boolean_expression
           | imported_command

?boolean_expression: or_expression
?or_expression: [or_expression OR] and_expression
?and_expression: [and_expression AND] not_expression
?not_expression: compare_expression
               | NOT not_expression
?compare_expression: [compare_expression compare_operator] number_expression
?compare_operator: EQUALS
                 | GORE
                 | LORE
                 | LESS
                 | GREATER
                 | NEQUALS

?number_expression: sum_expression
?sum_expression: [sum_expression (ADD | SUBTRACT)] product_expression
?product_expression: [product_expression (MULTIPLY | DIVIDE | ROUNDDIV | MODULO)] exponent_expression
// Exponentiation right to left associative
?exponent_expression: unary_expression [EXPONENT exponent_expression]
?unary_expression: value
                 | SUBTRACT unary_expression
value: NUMBER
     | BOOLEAN
     | STRING
     | NAME
     | "(" expression ")"
     | function_callback
     | function_callback_quirky
     | function_callback_quirky_pipe
     | char
     | tupleval

//constants
BOOLEAN: ("true" | "false")
COMMENT: "//" /[^\n]/*
OR: "||"
AND: "&&"
EQUALS: ("==" | "=")
GORE: ">="
LORE: "<="
LESS: "<"
GREATER: ">"
NEQUALS: ("/=" | "!=")
NOT: ("~" | "!")
ADD: "+"
SUBTRACT: "-"
MULTIPLY: "*"
DIVIDE: "/"
ROUNDDIV: "//"
MODULO: "%"
EXPONENT: "^"
%import common.ESCAPED_STRING -> STRING
%import common.SIGNED_NUMBER  -> NUMBER
%import common.CNAME  -> NAME
%import common.WS
%ignore WS
%ignore COMMENT
